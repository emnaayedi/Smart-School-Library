apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.slensky.focussis"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 26
        versionName "1.3.5"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        config {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {

                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "Focus"
        outputFileName += "-v" + android.defaultConfig.versionName
        if (variant.buildType.name == "release") {
            outputFileName += ".apk"
        } else {
            outputFileName += "-SNAPSHOT.apk"
        }
    }
}

dependencies {
    def lifecycle_version = '2.0.0-beta01'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation 'ch.acra:acra:4.9.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.firebase:firebase-database:18.0.1'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    implementation 'joda-time:joda-time:2.6'
    implementation 'com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:1.6.0'
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.takisoft.fix:preference-v7:28.0.0.0-alpha3'
    implementation 'com.github.medyo:android-about-page:1.2.1'
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.joestelmach:natty:0.11'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.karumi:dexter:4.2.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.0'
    implementation('com.google.api-client:google-api-client-android:1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-calendar:v3-rev315-1.23.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'me.xdrop:fuzzywuzzy:1.1.10'
    testImplementation 'junit:junit:4.12'
    implementation project(':MaterialCalendarView')

    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'


}

configurations {
    all {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
}
